apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    google()
    maven {
        url 'https://jitpack.io'
        content {
            includeModule('com.github.IvanShafran', 'shared-preferences-mock')
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.8'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.10.5'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.exparity:hamcrest-date:2.0.7'
    testImplementation 'com.github.IvanShafran:shared-preferences-mock:1.1'
}

android {
    compileSdkVersion 31

    defaultConfig {
        //Given a version number MAJOR.MINOR.PATCH, increment the:
        //
        //MAJOR version when you make incompatible API changes,
        //MINOR version when you add functionality in a backwards compatible manner, and
        //PATCH version when you make backwards compatible bug fixes.
        //
        //Additional labels for pre-release and build metadata are available as extensions to the MAJOR.MINOR.PATCH format.
        versionCode 89
        versionName "74.5.2"
        minSdkVersion 21
        targetSdkVersion 31
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        encoding = "UTF-8"
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/README'
        exclude 'META-INF/README.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    testOptions {
        // workaround for the error "Method i in android.util.Log not mocked" (http://tools.android.com/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-)
        // without PowerMockito it is impossible to mock Log.
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}
